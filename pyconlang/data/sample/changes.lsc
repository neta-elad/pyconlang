# +--------------+
# | ~ Features ~ |
# +--------------+
Feature voicing                 (voiceless, voiced)
Feature place                   (labial, dental, alveolar, postalveolar, retroflex, palatal, velar, uvular, pharyngeal, glottal)
Feature manner                  (nasal, stop, trill, tap, fricative, affricate, approximant)
Feature type                    (*consonant, vowel)
Feature +lateral

Feature +labialized
Feature +palatalized
Feature +velarized
Feature +pharyngealized
Feature +aspirated

Feature height                  (low, mid, high)
Feature frontness               (front, central, back)

Feature +round
Feature +long
Feature +nasalized

Feature (syllable) stress       (*unstressed, primary, secondary)
Feature (syllable) rhyme        (open, semiopen, closed)

Feature +macron

# +----------------+
# | ~ Diacritics ~ |
# +----------------+
Diacritic ʷ [+labialized]       (floating)
Diacritic ʲ [+palatalized]      (floating)
Diacritic ˠ [+velarized]        (floating)
Diacritic ˤ [+pharyngealized]   (floating)
Diacritic ʰ [+aspirated]        (floating)

Diacritic ː [+long]             (floating)
Diacritic  ̄ [+macron]
Diacritic  ̴ [+nasalized]        (floating)

Diacritic ˈ [primary]           (before)
Diacritic ˌ [secondary]         (before)

Diacritic ˚ [open]
Diacritic ˘ [semiopen]
Diacritic ˙ [closed]

# +----------------+
# | ~ Consonants ~ |
# +----------------+
# Labial
Symbol m [voiced labial nasal]
Symbol p [voiceless labial stop]
Symbol b [voiced labial stop]
Symbol ʙ [voiced labial trill]
Symbol ⱱ [voiced labial tap]
Symbol f [voiceless labial fricative]
Symbol v [voiced labial fricative]
Symbol p͡f [voiceless labial affricate]
Symbol b͡v [voiced labial affricate]
Symbol ʋ [voiced labial approximant]
Symbol ʍ [voiceless labial +velarized approximant]
Symbol w [voiced labial +velarized approximant]

# Dental
Symbol θ [voiceless dental fricative]
Symbol ð [voiced dental fricative]

# Alveolar
Symbol n [voiced alveolar nasal]
Symbol t [voiceless alveolar stop]
Symbol d [voiced alveolar stop]
Symbol r [voiced alveolar trill]
Symbol ɾ [voiced alveolar tap]
Symbol s [voiceless alveolar fricative]
Symbol z [voiced alveolar fricative]
Symbol ɬ [voiceless alveolar +lateral fricative]
Symbol ɮ [voiced alveolar +lateral fricative]
Symbol t͡s [voiceless alveolar affricate]
Symbol d͡z [voiced alveolar affricate]
Symbol t͡ɬ [voiceless alveolar +lateral affricate]
Symbol d͡ɮ [voiced alveolar +lateral affricate]
Symbol ɹ [voiced alveolar approximant]
Symbol l [voiced alveolar +lateral approximant]

# Post-alveolar
Symbol ʃ [voiceless postalveolar fricative]
Symbol ʒ [voiced postalveolar fricative]
Symbol t͡ʃ [voiceless postalveolar affricate]
Symbol d͡ʒ [voiced postalveolar affricate]

# Retroflex
Symbol ɳ [voiced retroflex nasal]
Symbol ʈ [voiceless retroflex stop]
Symbol ɖ [voiced retroflex stop]
Symbol ɽ [voiced retroflex tap]
Symbol ʂ [voiceless retroflex fricative]
Symbol ʐ [voiced retroflex fricative]
Symbol ɻ [voiced retroflex approximant]
Symbol ɭ [voiced retroflex +lateral approximant]

# Palatal
Symbol ɲ [voiced palatal nasal]
Symbol c [voiceless palatal stop]
Symbol ɟ [voiced palatal stop]
Symbol ç [voiceless palatal fricative]
Symbol ʝ [voiced palatal fricative]
Symbol j [voiced palatal approximant]
Symbol ʎ [voiced palatal +lateral approximant]

# Velar
Symbol ŋ [voiced velar nasal]
Symbol k [voiceless velar stop]
Symbol g [voiced velar stop]
Symbol x [voiceless velar fricative]
Symbol ɣ [voiced velar fricative]
Symbol ɰ [voiced velar approximant]
Symbol ʟ [voiced velar +lateral approximant]

# Uvular
Symbol ɴ [voiced uvular nasal]
Symbol q [voiceless uvular stop]
Symbol ɢ [voiced uvular stop]
Symbol ʀ [voiced uvular trill]
Symbol χ [voiceless uvular fricative]
Symbol ʁ [voiced uvular fricative]

# Pharyngeal
Symbol ħ [voiceless pharyngeal fricative]
Symbol ʕ [voiced pharyngeal fricative]

# Glottal
Symbol ʔ [voiceless glottal stop]
Symbol h [voiceless glottal fricative]
Symbol ɦ [voiced glottal fricative]

# +------------+
# | ~ Vowels ~ |
# +------------+
# High
Symbol i [high front -round vowel]
Symbol y [high front +round vowel]
Symbol ɨ [high central -round vowel]
Symbol ʉ [high central +round vowel]
Symbol ɯ [high back -round vowel]
Symbol u [high back +round vowel]

# Mid
Symbol e [mid front -round vowel]
Symbol ø [mid front +round vowel]
Symbol ə [mid central -round vowel]
Symbol ɤ [mid back -round vowel]
Symbol o [mid back +round vowel]

# Low
Symbol ɛ [low front -round vowel]
Symbol œ [low front +round vowel]
Symbol a [low central -round vowel]
Symbol ɶ [low central +round vowel]
Symbol ɑ [low back -round vowel]
Symbol ɒ [low back +round vowel]

# +-------------+
# | ~ Classes ~ |
# +-------------+
Class rhotics       {r, ɾ, ɹ, ɻ, ʀ, ʁ}
Class sibilants     {s, z, ʃ, ʒ}
Class glides        {w, j}

# +--------------+
# | ~ Elements ~ |
# +--------------+
Element liquids     {@rhotics, [+lateral approximant]}
Element sonorants   {[nasal], @liquids}
Element obstruents  {[stop], [fricative], [affricate]}

syllables:
    [consonant]? [vowel]

# go back to stress on vowels?..

initial-stress ltr: 
    <syl> => [primary] / $ _ $
    <syl> => [primary] / $ _ <syl> $
    <syl> => [primary] / _ [consonant] [vowel *long] <syl> $
    <syl> => [primary] / <syl>&![primary] _ <syl> $

vowel-loss-between-voiceless-obstruents:
    [unstressed vowel] => * / [vowel] @obstruents&[voiceless] _ @obstruents&[voiceless]

syllables:
    [consonant]? [vowel] (@obstruents&[voiceless])?

h-loss-between-vowels:
    h => * / [vowel] _ [vowel]

unstressed-vowels-become-glides:
    {i, u}&[unstressed] => {j, w} / {_ [vowel], [vowel] _}

syllables:
    [consonant]? @glides? [vowel] @glides? (@obstruents&[voiceless])?

stops-become-voiced-between-vowels:
    [stop] => [voiced] / [vowel] _ [vowel]

coda-stops-loss:
    [vowel] [stop] => [+long] * / _ . [stop]

h-loss-everywhere:
    [consonant]$1 . h => [] . $1
    [vowel] h => [+long] * / _ .
    h => *

vowel-loss-between-nasals-and-obstruents:
    [unstressed vowel] => * / [] [nasal] _ @obstruents

syllables:
    [consonant]? @glides? [vowel] @glides? {@obstruents&[voiceless], [nasal]}?

nasal-assimilation:
    [nasal] => [nasal $place] / _ [$place consonant]

palatalization:
    {{t, k, t͡s}, ʃ} j => {t͡ʃ, ʃ} *

word-final-vowel-simplification rtl:
    [unstressed *long vowel] => * / _ $ // [consonant] [consonant] _
    [unstressed +long vowel] => [*long] / _ $ // [consonant] [consonant] _
    [vowel] {[unstressed vowel *long], @glides} => [+long] * / _ $ // [consonant] [consonant] _

syllables:
    [consonant]? @glides? [vowel] @glides? {@obstruents&[voiceless], [nasal]}?
    [consonant]? @glides? [vowel] @glides? [consonant]? / _ $

romanizer-phonetic:
    unchanged

syllables: 
    clear

romanizer:
    j => y
    ɬ => hl
    t͡s => ts
    t͡ɬ => tl
    ʃ => sh
    t͡ʃ => ch
    ŋ => n
    [vowel +long] => [*long +macron]
